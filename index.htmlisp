(mapcar #'ql:quickload '(:markup :hunchentoot :parenscript :postmodern))
(defpackage retro-games
  (:use :cl :markup :hunchentoot :parenscript :postmodern))
(in-package retro-games)
(markup:enable-reader)
;; enter value of 'password in the debugger
(connect-toplevel "retro_games" "retro" 'password "localhost")

(defclass game ()
  ((name :col-type string
         :reader namep
         :initarg :name)
   (votes :col-type integer
          :accessor votes
          :initform 0))
  (:metaclass dao-class)
  (:keys name))
(defmethod vote-for ((user-selected-game game))
  "Increment the votes for USER-SELECTED-GAME."
  (incf (votes user-selected-game))
  (update-dao user-selected-game))
(defmethod unvote-for ((previous-game game))
  "decrement the votes for previous-game."
  (decf (votes previous-game))
  (update-dao previous-game))
(defmethod obj-equal ((a game) (b game))
  (string= (name a) (name b)))
(defmethod print-object ((obj game) out)
  (print-unreadable-object (obj out)
    (format out "GAME: ~S ~a" (name obj) (votes obj))))
;; continue if the table already exists
(execute (dao-table-definition 'game))

(defconstant retro-games "retro-games"
  "The name of the cookies from retro-games.")

(defun game-from-name (name)
  "Return game with NAME or nil is the game doesn't exist."
  (get-dao 'game name))

(defun game-stored-p (game-name)
  "true if there is a game with game-name."
  (game-from-name game-name))

(defun games ()
  "Return the list of games sorted by vote."
  (reverse (select-dao 'game t 'votes)))

(defun add-game (name)
  "Add a game with NAME and zero votes."
  (unless (game-stored-p name)
    (insert-dao (make-instance 'game :name name))))

(defun remove-game (name)
  "Remove the game with NAME."
  (let ((game (game-from-name name)))
    (when game
      (delete-dao game))))

(deftag standard-page (children &key title)
  <html>
    <head>
      <title>,(progn title)</title>
      <link type="text/css" rel="stylesheet" href="/retro.css">
    </head>
    <body>
      <header>
        <span class="strapline">Vote on your favorite retro game</span>
      </header>
      <div id="content">
        ,@(progn children)
      </div>
      <hr>
      <footer>
        <img id="made-with-lisp" src="/made-with-lisp.png" />
      </footer>
    </body>
  </html>)

;; root
(define-easy-handler (retro-games :uri "/" ) ()
  (start-session)
  (let ((value (cookie-in retro-games)))
    (when (and value (not (game-stored-p value)))
      (set-cookie retro-games :value ""))
    (write-html
     <standard-page title="Retro Games">
       <h1>Top Retro Games</h1>
       <p>Is your favorite game not here? Make it available for votes <a href="/new-game">here</a>.</p>
       ,(when (and value (game-stored-p value))
          <p style="color:gray;">
            You already voted for ,(progn value). If you vote again, your vote will be transfered.
          </p>)
       <ol id="chart">
         ,@(mapcar
            (lambda (game)
              <li>
                <a href=(format nil "/vote?name=~a" (url-encode (name game)))>Vote!</a>
                ,(format nil "~a with ~d votes" (name game) (votes game))
              </li>)
            (games))
       </ol>
     </standard-page>)))

(define-easy-handler (vote :uri "/vote") (name)
  (when name
    (let* ((game (game-from-name name))
           (value (cookie-in retro-games))
           (prev-game (when value (game-from-name value))))
      (when (or (null prev-game)
                (not (obj-equal game prev-game)))
        (when prev-game
          (unvote-for prev-game))
        (vote-for game)
        (set-cookie retro-games :value name))))
  (redirect "/"))

(define-easy-handler (new-game :uri "/new-game") ()
  (write-html
   <standard-page title="New Game">
     <h1>Add New Game to the Chart</h1>
     <form action="/game-added" method="post"
           onsubmit=(ps-inline
                        (when (= ((@ name value trim)) "")
                          (setf (inner-html (get-element-by-id "error")) "Please enter a name")
                          (return false))) >
       <p style="color:gray;">Only enter games that are really <em>retro</em> games.</p>
       <label for="game-name">What is the name of the game?</label>
       <input id="game-name" name="name" class="txt" autofocus
              oninput=(ps-inline
                          (setf (inner-html (get-element-by-id "error")) "")) /> 
       <span id="error" style="color:red;"></span>
       <input type="submit" value="Add" class="btn" />
     </form>
   </standard-page>))

(define-easy-handler (game-added :uri "/game-added") (name)
  (let ((name (str:trim name)))
    (unless (or (null name) (zerop (length name)))
      (add-game name)))
  (redirect "/"))

(defvar server (start (make-instance 'easy-acceptor :port 8080 :document-root "./")))

